WHITESPACE = _{ " " }
// Dice definitions
dice_number = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
dice_sides = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
numbered_dice = ${ dice_number? ~ ^"D" ~ dice_sides }
fudge_dice = ${ dice_number? ~ ^"F" ~ !(ASCII_ALPHANUMERIC)} // Remove ambiguity with "Flip" by forbidding any following alphanumeric character
num_const_dice = ${ "+" ~ dice_sides }
dice = { numbered_dice | fudge_dice | num_const_dice }
dice_and_action = { dice | ("(" ~ dice ~ action* ~ ")") }

// Actions definition
action_sum = @{ ^"Sum" }
action_flip = @{ ^"Flip" }
action_total = @{ ^"Total" }

factor = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
action_mult = ${ ^"x" ~ factor }

num_roll_value = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
fudge_roll_value = @{ "+" | "-" | "0" }
action_reroll = ${ ^"Reroll(" ~ ((num_roll_value ~ ("," ~ num_roll_value)* ) | (fudge_roll_value ~ ("," ~ fudge_roll_value)* )) ~ ")" }
action_explode = ${ ^"Explode(" ~ ((num_roll_value ~ ("," ~ num_roll_value)* ) | (fudge_roll_value ~ ("," ~ fudge_roll_value)* )) ~ ")" }
action = ${ action_sum | action_flip | action_total | action_mult | action_explode | action_reroll }

aggregation_count = @{ ^"Count" }
aggregation = ${ aggregation_count }

roll_request = { SOI ~ dice_and_action+ ~ action* ~ aggregation? ~ EOI }